
INHERITS CTagv2Frame

PROPERTY encoding AS Byte
PROPERTY filename AS String
PROPERTY READ MIMEType AS String
PROPERTY description AS String
PROPERTY READ content AS String

PRIVATE $encoding AS Byte = &H03
PRIVATE $mime AS String
PRIVATE $path AS String
PRIVATE $desc AS String
PRIVATE $frameSeek AS Integer

PUBLIC SUB _new(OPTIONAL rawData AS String = NULL, OPTIONAL path AS String = NULL)
  
  DIM x AS Integer = Len(rawData)
  
  IF IsNull(rawData) THEN 
    IF Exist(path) THEN $path = path
  ELSE 
    $encoding = Asc(rawData)
    rawData = Mid(rawData, 2)
    
    WHILE Asc(rawData) > 0
      $mime &= Left(rawData)
      rawData = Mid(rawData, 2)
    WEND 
    rawData = Mid(rawData, 2)
    
    WHILE Asc(rawData) > 0
      $path &= Left(rawData)
      rawData = Mid(rawData, 2)
    WEND 
    rawData = Mid(rawData, 2)
    
    WHILE Asc(rawData) > 0
      $desc &= Left(rawData)
      rawData = Mid(rawData, 2)
    WEND 
    rawData = Mid(rawData, 2)
    
    $frameSeek = x - Len(rawData)
  END IF
  
END

PUBLIC FUNCTION render() AS String
  
  DIM result AS String = SUPER.header.render()
  
  result &= Chr($encoding)
  result &= ME.MIMEType & CTagv2FrameText.encSplit[$encoding]
  result &= File.Name($path) & CTagv2FrameText.encSplit[$encoding]
  result &= $desc & CTagv2FrameText.encSplit[$encoding]
  result &= ME.content
  RETURN result
  
END


PRIVATE FUNCTION encoding_Read() AS Byte
  RETURN $encoding
END
PRIVATE SUB encoding_Write(Value AS Byte)
  $encoding = Value
END
PRIVATE FUNCTION filename_Read() AS String
  RETURN $path
END
PRIVATE SUB filename_Write(Value AS String)
  IF Exist(Value) THEN $path = Value
END
PRIVATE FUNCTION description_Read() AS String
  RETURN $desc
END
PRIVATE SUB description_Write(Value AS String)
  $desc = Value
END
PRIVATE FUNCTION content_Read() AS String
  IF Len($path) > 0 THEN RETURN File.Load($path)
  RETURN SUPER.parent.parent.parent.getFromFile(SUPER.parent.header.fStart + SUPER.header.tStart + $frameSeek, SUPER.header.size - $frameSeek)
END
PRIVATE FUNCTION MIMEType_Read() AS String
  RETURN $mime
END
